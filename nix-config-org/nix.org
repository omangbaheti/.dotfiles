#+TITLE: Nix Config
#+DESCRIPTION: Personal Config
#+STARTUP: showeverything
#+OPTIONS: toc:3

* Table of Contents :toc_4:
- [[#flake-structure][Flake Structure]]
  - [[#flake-inputs][Flake Inputs]]
  - [[#flake-outputs][Flake Outputs]]
  - [[#helper-functions-for-system-and-home-maanger-config][Helper Functions for System and Home Maanger Config]]
- [[#common-configuration][Common Configuration]]
  - [[#fonts][Fonts]]
  - [[#software][Software]]
  - [[#emacs-configuration][Emacs Configuration]]
- [[#wsl-config-files][WSL Config Files]]
  - [[#configurationnix][Configuration.nix]]
  - [[#homenix][Home.nix]]
- [[#zsh-config][ZSH Config]]
  - [[#wsl-config][WSL Config]]

* Flake Structure
#+begin_src nix :tangle flake.nix :noweb yes
{
description = "Unified Flake Config";
<<flake-input>>
<<flake-output>>
}
#+end_src

** Flake Inputs 
#+name: flake-input
#+begin_src nix 
inputs = 
  {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    nixpkgs-stable.url = "github:NixOS/nixpkgs/nixos-25.05";
    nixos-wsl = 
      {
        url = "github:nix-community/NixOS-WSL";
        inputs.nixpkgs.follows = "nixpkgs";
      };

    home-manager = 
      {
        url = "github:nix-community/home-manager/master";
        inputs.nixpkgs.follows = "nixpkgs";
      };
  };
#+end_src 

** Flake Outputs
#+name: flake-output
#+begin_src nix :noweb yes 
outputs = { self, nixpkgs, nixpkgs-stable, nixos-wsl, home-manager, ... }@inputs: 
  let
    stableFor = system:
      nixpkgs-stable.legacyPackages.${system};
    commonSettings = 
      {
        dotfilesDir = ".dotfiles";
        allowUnfree = true;
        editor = "emacs";
        browser = "firefox";
      };
    
    #defining variables for each of my machines
    machines = 
      {
        wsl = commonSettings //  # // is used to merge and pull in shared values
              {
                system = "x86_64-linux";
                host = "nixos";
                username = "nixos";
                systemType = "wsl";
              }; 

        fern = commonSettings // 
               {
                 system = "x86_64-linux";
                 host = "fern";
                 username = "fern";
                 systemType = "native";
               };

        nyx = commonSettings // 
              {
                system = "x86_64-linux";
                host = "nyx";
                username = "nyx";
                systemType = "native";
              };

        sakura = (commonSettings // # overriding system when required
                  {
                    system = "aarch64-linux";
                    host = "fern";
                    username = "fern";
                    systemType = "native_pi";
                  });
      };
    <<makeSystem>> 
    
    <<makeHome>>
  in
    {
      nixosConfigurations = 
        {
          nix-wsl= mkSystem machines.wsl;
          fern   = mkSystem machines.fern;
          nyx    = mkSystem machines.nyx;
          sakura = mkSystem machines.sakura;
        };
      homeConfigurations = 
        {
          "${machines.wsl.username}@${machines.wsl.host}" = mkHome machines.wsl;
          #"${machines.fern.username}@${machines.fern.host}" = mkHome machines.fern;
          #"${machines.nyx.username}@${machines.nyx.host}" = mkHome machines.nyx;
          #"${machines.sakura.username}@${machines.sakura.host}" = mkHome machines.sakura;
        };
    };
#+end_src

** Helper Functions for System and Home Maanger Config
#+name: makeSystem
#+begin_src nix
mkSystem = machine:
  let
    pkgs = nixpkgs.legacyPackages.${machine.system};
    isWSL = machine.systemType == "wsl";
  in
    nixpkgs.lib.nixosSystem 
      {
        system = machine.system;
        modules = 
          [
            (if isWSL then nixos-wsl.nixosModules.wsl else {})
            home-manager.nixosModules.home-manager
            
            # Forward args into HM modules here:
            ({ ... }: {
              home-manager.extraSpecialArgs = {
                inherit machine;
                stable = stableFor machine.system;
                host = machine.host;
                username = machine.username;
                systemType = machine.systemType;
                machines = machines;
              };

              home-manager.users.${machine.username} = import ./${machine.host}/home.nix;
            })

            ./${machine.host}/configuration.nix
          ];

        specialArgs = {
          machine = machine;
          stable = stableFor machine.system;
          host = machine.host;
          username =  machine.username;
          systemType = machine.systemType;
        };
      };
#+end_src 

#+name: makeHome
#+begin_src nix
mkHome = machine:
  home-manager.lib.homeManagerConfiguration {
    pkgs = nixpkgs.legacyPackages.${machine.system};

    extraSpecialArgs = 
      {
        machine = machine;
        stable = stableFor machine.system;
        host = machine.host;
        username = machine.username;
        systemType = machine.systemType;
      };

    modules = [
      ({ ... }: {
        home.username = machine.username;
        home.homeDirectory = "/home/${machine.username}";
      })

      # Put your actual HM module(s) here
      ./${machine.host}/home.nix
    ];
  };
#+end_src 

* Common Configuration 
** Fonts
Embedding this src block in each config instead of its own module. 

#+name:fonts
#+begin_src nix
fonts.packages = with pkgs.nerd-fonts; 
  [ 
    jetbrains-mono
    ubuntu
    fira-code
    fira-mono
    dejavu-sans-mono
    symbols-only
  ];
#+end_src

** Software
Stuff that i need on every device

#+begin_src nix :tangle ./modules/common-packages.nix
{ config, pkgs, stable, ... }:
{
  nix.settings.experimental-features = ["nix-command" "flakes"];
  environment.systemPackages = with pkgs;
    [
      #cli tools
      
      git
      git-lfs
      lazygit
      wiper
      zoxide
      fzf
      vim 
      zsh
      oh-my-posh
      tealdeer
      btop
      #nnn  
      fd
      eza
      devenv
      nix-direnv
      yazi
      diff-so-fancy
      fastfetch
      ripgrep
      poetry
      pandoc
      
      #LSPs / Compilers / Dev Envs
      #C/C++
      glib
      gdb
      clang
      cmake
      gcc
      libvterm
      libgcc
      libtool
     
      #python 
      pylint
      basedpyright
      pyrefly
     
      #node 
      nodejs_24
      
      #openjdk
      zulu8 
      
      # C#
      omnisharp-roslyn
      
      #Latex LSP and grammar checking
      (aspellWithDicts (dicts: with dicts; [ en en-computers en-science ]))
      proselint
      harper
      ltex-ls-plus
      texlab
      texliveFull
     
      #Nix 
      nixfmt-rfc-style
      statix
      nil
      
      #misc    
      home-manager
      languagetool
      
      #misc     
      firefox
      coreutils
      syncthing
      hugo
      mupdf
    ];
}
#+end_src

** Emacs Configuration
#+name:emacs-config-wayland
#+begin_src nix
  services.emacs = {
    enable = true;
  };

  programs.emacs = {
    enable = true;
    package = pkgs.emacs-pgtk;
    extraPackages = (
      epkgs: with pkgs.emacsPackages;   
        [ 
          vterm 
          zmq 
          treesit-auto
          treesit-grammars.with-all-grammars
          pdf-tools
          # (melpaBuild {
          #   ename = "reader";
          #   pname = "emacs-reader";
          #   version = "20250630";
          #   src = pkgs.fetchFromGitea {
          #     domain = "codeberg.org";
          #     owner = "divyaranjan";
          #     repo = "emacs-reader";
          #     rev = "0.3.2"; # replace with 'tag' for stable
          #     hash = "sha256-BpuWWGt46BVgQZPHzeLEbzT+ooR4v29R+1Lv0K55kK8=";
          #   };
          #   files = ''(:defaults "render-core.so")'';
          #   nativeBuildInputs = [ pkgs.pkg-config ];
          #   buildInputs = [ pkgs.gcc pkgs.mupdf pkgs.gnumake pkgs.pkg-config ];
          #   preBuild = "make clean all";
          # })

        ]
    );
  };
#+end_src

* WSL Config Files

** Configuration.nix
#+begin_src nix :tangle ./nixos/configuration.nix :noweb yes
# Edit this configuration file to define what should be installed on
# your system. Help is available in the configuration.nix(5) man page, on
# https://search.nixos.org/options and in the NixOS manual (`nixos-help`).

# NixOS-WSL specific options are documented on the NixOS-WSL repository:
# https://github.com/nix-community/NixOS-WSL

{config, lib, pkgs, stable, machine, ... }:
let
  userName = machine.username;
  allowUnfree = machine.allowUnfree;
in
{
  nix.settings.trusted-users = [ "root" userName];
  nixpkgs.config.allowUnfree = allowUnfree;
  
  wsl.enable = true;
  wsl.defaultUser = userName;
  
  imports = 
    [
      ../modules/common-packages.nix 
    ];
  
  <<fonts>>
  
  programs.zsh.enable = true;
  users.defaultUserShell = pkgs.zsh;

  environment.systemPackages = with pkgs;
    [
      gtk4
    ];

  # This value determines the NixOS release from which the default
  # settings for stateful data, like file locations and database versions
  # on your system were taken. It's perfectly fine and recommended to leave
  # this value at the release version of the first install of this system.
  # Before changing this value read the documentation for this option
  # (e.g. man configuration.nix or on https://nixos.org/nixos/options.html).
  system.stateVersion = "24.11"; # Did you read the comment?
}

#+end_src


** Home.nix
#+begin_src nix :tangle ./nixos/home.nix :noweb yes   
{ config, pkgs, stable, machine, ... }:
{
  # Home Manager needs a bit of information about you and the
  # paths it should manage.
  # home.username = machine.username; 
  # home.homeDirectory = /home/${machine.username}; # Replace with your username
  
  home.packages = with pkgs; 
    [
      cmake
      glib
      dconf
    ];
  
  # Let Home Manager install and manage itself
  programs.home-manager.enable = true;
  
  # Git configuration
  programs.git = {
    enable = true;
    settings = {
      user.name = "Omang Baheti";
      user.email = "omangbaheti@gmail.com";
      init.defaultBranch = "main";
      push.default = "simple";
    };
  };

  # Zsh configuration
  programs.zsh = 
    {
      enable = true;
      enableCompletion = true;
      autosuggestion.enable = true;
      syntaxHighlighting.enable = true;

      shellAliases = 
        {
          ll = "eza -l";
          la = "eza -la";
          ls = "eza";
          grep = "rg";
          find = "fd";
          e="emacsclient -c";
          update-config = "sudo nixos-rebuild switch --flake .#nixos";
          exp="/mnt/c/WINDOWS/explorer.exe .";
        };

      oh-my-zsh = 
        {
          enable = true;
          plugins = [ "git" "sudo" ];
          theme = "robbyrussell";
        };

      initContent = ''
<<zshConfigWsl>>
    '';
    };

  services.ssh-agent.enable = true;
  
  programs.ssh = {
    enable = true;

    matchBlocks."github.com" = {
      identityFile = "~/.ssh/id_ed25519";
      addKeysToAgent = "yes";
      forwardAgent = true;
    };
  };

  # programs.ssh = 
  #   {
  #     enable = true;
  #     addKeysToAgent = "yes";       # Automatically adds keys to the agent
  #     forwardAgent = true;         # Optional: disables forwarding
  #     extraConfig = ''
  #   Host github.com
  #     IdentityFile ~/.ssh/id_ed25519
  # '';
  #   };
  
  programs.keychain = 
    {
      enable = true;
      keys = [ "id_ed25519" ];  # Replace with your SSH key filename
      enableZshIntegration = true;
    };
  
  # Direnv for automatic environment loading
  programs.direnv = 
    {
      enable = true;
      enableZshIntegration = true;
      nix-direnv.enable = true;
      
      config = {
        global = {
          log_format = "-";
          log_filter = "^$";
          hide_env_diff = true;
        };
      };
    };


  <<emacs-config-wayland>>

  services.syncthing = 
    {
      enable = true;
    };

  gtk = 
    {
      enable = true;
      theme = 
        {
          package = pkgs.orchis-theme;
          name = "Orchis-Dark"; # or "Orchis-Dark", "Orchis-Purple", etc.
        };
    };

  home.sessionVariables = 
    {
      EMACSLOADINIT = "${config.home.homeDirectory}/${machine.dotfilesDir}/emacs/init.el";
      GTK_THEME = "Orchis-Dark";
    };
  
  home.file.".emacs.d/init.el".source = /${config.home.homeDirectory}/${machine.dotfilesDir}/emacs/init.el;
  # home.file.".emacs.d/init.el".source = ../emacs/init.el;
  dconf.settings."org/gnome/desktop/wm/preferences".button-layout = ":minimize,maximize,close";

  # This value determines the Home Manager release that your
  # configuration is compatible with.
  home.stateVersion = "24.11"; # Don't change this
}
#+end_src

* ZSH Config
** WSL Config
#+name: zshConfigWsl
#+begin_src bash
   #DISPLAY=$(ip route | grep default | awk '{print $3}'):0.0
   #LIBGL_ALWAYS_INDIRECT=1  
   eval "$(direnv hook zsh)"
   export R_LIBS_SITE=$(ls -d /nix/store/*-r-*/library | tr '\n' ':')

   PATH=/nix/store/5qng39wihv3lfgr03cf7mqbg4lpf4m45-cmake-3.30.5/bin:/mnt/c/Windows/System32/WindowsPowerShell/v1.0:$PATH
     eval "$(zoxide init zsh)"
     eval "$(oh-my-posh init zsh)"

     function isWinDir 
     {
        case "$PWD/" in
        /mnt/*) return 0 ;;
        ,*) return 1 ;;
        esac
     }

     function lazygit 
     {
        if isWinDir; then
     #  Use Windows `lazygit.exe` in Windows-mounted dirs
            command lazygit.exe "$@"
        else
     #  Use native Linux `lazygit` in native dirs
            command lazygit "$@"
        fi 
     }
#+end_src

