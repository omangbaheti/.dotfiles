#+TITLE: Nix Config
#+DESCRIPTION: Personal Config
#+STARTUP: showeverything
#+OPTIONS: toc:3

* Table of Contents :toc_4:
- [[#flake-structure][Flake Structure]]
  - [[#flake-inputs][Flake Inputs]]
  - [[#flake-outputs][Flake Outputs]]
  - [[#helper-functions-for-system-and-home-maanger-config][Helper Functions for System and Home Maanger Config]]
- [[#common-configuration][Common Configuration]]
  - [[#fonts][Fonts]]
  - [[#software][Software]]
  - [[#emacs-configuration][Emacs Configuration]]
  - [[#common-home-manager-configuration][Common Home Manager Configuration]]
- [[#individual-configurations][Individual Configurations]]
  - [[#wsl-config-files][WSL Config Files]]
    - [[#configurationnix][Configuration.nix]]
    - [[#homenix][Home.nix]]
  - [[#fern---t490][Fern - T490]]
    - [[#configurationnix-1][Configuration.nix]]
    - [[#homenix-1][Home.nix]]
- [[#zsh-config][ZSH Config]]
  - [[#wsl-config][WSL Config]]
  - [[#fern-config][Fern Config]]

* Flake Structure
#+begin_src nix :tangle flake.nix :noweb yes
{
description = "Unified Flake Config";
<<flake-input>>
<<flake-output>>
}
#+end_src

** Flake Inputs 
#+name: flake-input
#+begin_src nix 
inputs = 
  {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    nixpkgs-stable.url = "github:NixOS/nixpkgs/nixos-25.05";
    nixos-wsl = 
      {
        url = "github:nix-community/NixOS-WSL";
        inputs.nixpkgs.follows = "nixpkgs";
      };

    home-manager = 
      {
        url = "github:nix-community/home-manager/master";
        inputs.nixpkgs.follows = "nixpkgs";
      };
  };
#+end_src 

** Flake Outputs
#+name: flake-output
#+begin_src nix :noweb yes 
outputs = { self, nixpkgs, nixpkgs-stable, nixos-wsl, home-manager, ... }@inputs: 
  let
    stableFor = system:
      nixpkgs-stable.legacyPackages.${system};
    commonSettings = 
      {
        dotfilesDir = ".dotfiles";
        allowUnfree = true;
        editor = "emacs";
        browser = "firefox";
      };
    
    #defining variables for each of my machines
    machines = 
      {
        wsl = commonSettings //  # // is used to merge and pull in shared values
              {
                system = "x86_64-linux";
                host = "nixos";
                username = "nixos";
                systemType = "nix-wsl"; #TODO: change host-name to nix-wsl and remove redundant use of systemType
              }; 

        fern = commonSettings // 
               {
                 system = "x86_64-linux";
                 host = "fern";
                 username = "fern";
                 systemType = "fern";
               };

        nyx = commonSettings // 
              {
                system = "x86_64-linux";
                host = "nyx";
                username = "nyx";
                systemType = "nyx";
              };

        sakura = (commonSettings // # overriding system when required
                  {
                    system = "aarch64-linux";
                    host = "sakura";
                    username = "sakura";
                    systemType = "pi";
                  });
      };
    <<makeSystem>> 
    
    <<makeHome>>
  in
    {
      nixosConfigurations = 
        {
          nix-wsl= mkSystem machines.wsl;
          fern   = mkSystem machines.fern;
          nyx    = mkSystem machines.nyx;
          sakura = mkSystem machines.sakura;
        };
      homeConfigurations = 
        {
          "${machines.wsl.username}@${machines.wsl.host}" = mkHome machines.wsl;
          "${machines.fern.username}@${machines.fern.host}" = mkHome machines.fern;
          "${machines.nyx.username}@${machines.nyx.host}" = mkHome machines.nyx;
          "${machines.sakura.username}@${machines.sakura.host}" = mkHome machines.sakura;
        };
    };
#+end_src

** Helper Functions for System and Home Maanger Config
#+name: makeSystem
#+begin_src nix
mkSystem = machine:
  let
    pkgs = nixpkgs.legacyPackages.${machine.system};
    isWSL = machine.systemType == "nix-wsl";
  in
    nixpkgs.lib.nixosSystem 
      {
        system = machine.system;
        modules = 
          [
            (if isWSL then nixos-wsl.nixosModules.wsl else {})
            home-manager.nixosModules.home-manager
            
            # Forward args into HM modules here:
            ({ ... }: {
              home-manager.extraSpecialArgs = {
                inherit machine;
                stable = stableFor machine.system;
                host = machine.host;
                username = machine.username;
                systemType = machine.systemType;
                machines = machines;
              };

              home-manager.users.${machine.username} = import ./${machine.host}/home.nix;
            })

            ./${machine.host}/configuration.nix
          ];

        specialArgs = {
          machine = machine;
          stable = stableFor machine.system;
          host = machine.host;
          username =  machine.username;
          systemType = machine.systemType;
        };
      };
#+end_src 

#+name: makeHome
#+begin_src nix
mkHome = machine:
  home-manager.lib.homeManagerConfiguration {
    pkgs = nixpkgs.legacyPackages.${machine.system};

    extraSpecialArgs = 
      {
        machine = machine;
        stable = stableFor machine.system;
        host = machine.host;
        username = machine.username;
        systemType = machine.systemType;
      };

    modules = [
      ({ ... }: {
        home.username = machine.username;
        home.homeDirectory = "/home/${machine.username}";
      })

      # Put your actual HM module(s) here
      ./${machine.host}/home.nix
    ];
  };
#+end_src 


* Common Configuration 
** Fonts

#+name:fonts
#+begin_src nix
fonts.packages = with pkgs.nerd-fonts; 
  [ 
    jetbrains-mono
    ubuntu
    fira-code
    fira-mono
    dejavu-sans-mono
    symbols-only
  ];
#+end_src

** Software
Stuff that i need on every device
#+begin_src nix :tangle ./modules/common-packages.nix
{ config, pkgs, stable, ... }:
let
  python-with-packages = pkgs.python3.withPackages (ps: with ps; [
    numpy
    pandas
    dill
    requests
    matplotlib
    scipy
    # Remove jupyter and jupyterlab - managed by services.jupyter
    seaborn
    xlib
    epc
    sexpdata
    six
    inflect
    pyqt6
    pyqt6-sip
    pyqt6-webengine
    qrcode
    python-lsp-server
    watchdog
    ipykernel  # Keep this for the kernel
  ]);
in
{
  nix.settings.experimental-features = ["nix-command" "flakes"];
  environment.systemPackages = with pkgs;
    [
      #cli tools
      git
      git-lfs
      lazygit
      wiper
      zoxide
      fzf
      vim 
      zsh
      oh-my-posh
      tealdeer
      btop
      #nnn  
      fd
      eza
      devenv
      nix-direnv
      yazi
      diff-so-fancy
      fastfetch
      ripgrep
      poetry
      pandoc
      
      #LSPs / Compilers / Dev Envs
      #C/C++
      glib
      gdb
      clang
      cmake
      gcc
      libvterm
      libgcc
      libtool
      
      #python 
      pylint
      basedpyright
      pyrefly
      python-with-packages
      
      #node 
      nodejs_24
      
      #openjdk
      zulu8 
      
      # C#
      omnisharp-roslyn
      
      #Latex LSP and grammar checking
      (aspellWithDicts (dicts: with dicts; [ en en-computers en-science ]))
      proselint
      harper
      ltex-ls-plus
      texlab
      texliveFull
      
      #Nix 
      nixfmt-rfc-style
      statix
      nil
      
      #misc    
      home-manager
      languagetool
      
      #misc     
      firefox
      coreutils
      syncthing
      hugo
      mupdf
    ];
}
#+end_src

** Emacs Configuration
#+name:emacs-config-wayland
#+begin_src nix
  services.emacs = {
    enable = true;
  };

  programs.emacs = {
    enable = true;
    package = pkgs.emacs-pgtk;
    extraPackages = (
      epkgs: with pkgs.emacsPackages;   
        [ 
          vterm 
          zmq 
          treesit-auto
          treesit-grammars.with-all-grammars
          pdf-tools
        ]
    );
  };
#+end_src

** Common Home Manager Configuration
#+name:common-home-config
#+begin_src nix
    programs.home-manager.enable = true;

    programs.zoxide.enable = true;
    programs.git = {
    enable = true;
    settings = {
        user.name = "Omang Baheti";
        user.email = "omangbaheti@gmail.com";
        init.defaultBranch = "main";
        push.default = "simple";
    };
    };

    programs.zsh = 
    {
        enable = true;
        enableCompletion = true;
        autosuggestion.enable = true;
        syntaxHighlighting.enable = true;

        shellAliases = 
        {
            ll = "eza -l";
            la = "eza -lah --tree";
            ls = "eza -h --git --icons --color=auto --group-directories-first -s extension";
            tree = "eza --tree --icons";
            grep = "rg";
            find = "fd";
            e="emacsclient -c";
            emd = "emacs --daemon";
            rebuild-config = "sudo nixos-rebuild switch --flake ~/.dotfiles/nix-config-org#${machine.systemType}";
            rebuild-home-config = "home-manager switch --flake  ~/.dotfiles/nix-config-org#${machine.username}@${machine.host}";
            exp="/mnt/c/WINDOWS/explorer.exe .";
        };

        oh-my-zsh = 
        {
            enable = true;
            plugins = [ "git" "sudo" ];
            theme = "robbyrussell";
        };
    };

    services.syncthing = 
    {
        enable = true;
    };
#+end_src

* Individual Configurations
** WSL Config Files
*** Configuration.nix
#+begin_src nix :tangle ./nixos/configuration.nix :noweb yes
# Edit this configuration file to define what should be installed on
# your system. Help is available in the configuration.nix(5) man page, on
# https://search.nixos.org/options and in the NixOS manual (`nixos-help`).

# NixOS-WSL specific options are documented on the NixOS-WSL repository:
# https://github.com/nix-community/NixOS-WSL

{config, lib, pkgs, stable, machine, ... }:
let
  userName = machine.username;
  allowUnfree = machine.allowUnfree;
in
{
  nix.settings.trusted-users = [ "root" userName];
  nixpkgs.config.allowUnfree = allowUnfree;
  
  wsl.enable = true;
  wsl.defaultUser = userName;
  
  imports = 
    [
      ../modules/common-packages.nix 
    ];
  
  <<fonts>>
  
  programs.zsh.enable = true;
  users.defaultUserShell = pkgs.zsh;

  environment.systemPackages = with pkgs;
    [
      gtk4
    ];

  # This value determines the NixOS release from which the default
  # settings for stateful data, like file locations and database versions
  # on your system were taken. It's perfectly fine and recommended to leave
  # this value at the release version of the first install of this system.
  # Before changing this value read the documentation for this option
  # (e.g. man configuration.nix or on https://nixos.org/nixos/options.html).
  system.stateVersion = "24.11"; # Did you read the comment?
}

#+end_src

*** Home.nix
#+begin_src nix :tangle ./nixos/home.nix :noweb yes   
{ config, pkgs, stable, machine, ... }:
{
  # Home Manager needs a bit of information about you and the
  # paths it should manage.
  # home.username = machine.username; 
  # home.homeDirectory = /home/${machine.username}; # Replace with your username
  
  home.packages = with pkgs; 
    [
      cmake
      glib
      dconf
    ];
  <<common-home-config>>  
  programs.zsh.initContent = 
    ''
 <<zshConfigWsl>>
'';

  services.ssh-agent.enable = true;
  
  programs.ssh = {
    enable = true;

    matchBlocks."github.com" = {
      identityFile = "~/.ssh/id_ed25519";
      addKeysToAgent = "yes";
      forwardAgent = true;
    };
  };

  programs.keychain = 
    {
      enable = true;
      keys = [ "id_ed25519" ];  # Replace with your SSH key filename
      enableZshIntegration = true;
    };
  
  # Direnv for automatic environment loading
  programs.direnv = 
    {
      enable = true;
      enableZshIntegration = true;
      nix-direnv.enable = true;
      
      config = {
        global = {
          log_format = "-";
          log_filter = "^$";
          hide_env_diff = true;
        };
      };
    };

  <<emacs-config-wayland>>
  gtk = 
    {
      enable = true;
      theme = 
        {
          package = pkgs.orchis-theme;
          name = "Orchis-Dark"; # or "Orchis-Dark", "Orchis-Purple", etc.
        };
    };

  home.sessionVariables = 
    {
      EMACSLOADINIT = "${config.home.homeDirectory}/${machine.dotfilesDir}/emacs/init.el";
      GTK_THEME = "Orchis-Dark";
    };
  
  home.file.".emacs.d/init.el".source = /${config.home.homeDirectory}/${machine.dotfilesDir}/emacs/init.el;
  # home.file.".emacs.d/init.el".source = ../emacs/init.el;
  dconf.settings."org/gnome/desktop/wm/preferences".button-layout = ":minimize,maximize,close";

  # This value determines the Home Manager release that your
  # configuration is compatible with.
  home.stateVersion = "24.11"; # Don't change this
}
#+end_src

** Fern - T490
*** Configuration.nix
#+begin_src nix :tangle ~/.dotfiles/nix-config-org/fern/configuration.nix
# Edit this configuration file to define what should be installed on
# your system.  Help is available in the configuration.nix(5) man page
# and in the NixOS manual (accessible by running ‘nixos-help’).

{ config, pkgs, stable, machine, ... }:
let
  hostname = machine.host;
  username = machine.username;
  allowUnfree = machine.allowUnfree;
in
{
  imports =
    [ # Include the results of the hardware scan.
      ./hardware-configuration.nix
      ../modules/common-packages.nix 
    ];

  # Bootloader.
  boot.loader.systemd-boot.enable = true;
  boot.loader.efi.canTouchEfiVariables = true;
  boot.kernelPackages = pkgs.linuxPackages;
  networking.hostName = hostname; # Define your hostname.
  #networking.wireless.enable = true;  # Enables wireless support via wpa_supplicant.

  # Configure network proxy if necessary
  # networking.proxy.default = "http://user:password@proxy:port/";
  # networking.proxy.noProxy = "127.0.0.1,localhost,internal.domain";
  
  # Enable networking
  networking.networkmanager.enable = true;
  # networking.useDHCP = false;
  # networking.interfaces.eno1.useDHCP = true;

  nix.settings.experimental-features = ["nix-command" "flakes"];

  nixpkgs.config.allowUnfree = machine.allowUnfree;

  # Set your time zone.
  # time.timeZone = systemSettings.timezone;

  # Select internationalisation properties.
  i18n.defaultLocale = systemSettings.defaultLocale;

  # Enable the X11 windowing system.
  services.xserver.enable = true;

  # Enable the GNOME Desktop Environment.
  services.xserver.displayManager.gdm.enable = true;
  services.xserver.desktopManager.gnome.enable = true;

  # Configure keymap in X11
  services.xserver = 
    {
      xkb.layout = "us";
      xkb.variant = "";
    };

  # Enable CUPS to print documents.
  services.printing.enable = true;

  # Enable sound with pipewire.
  services.pulseaudio.enable = false;
  security.rtkit.enable = true;
  services.power-profiles-daemon.enable = true;
  services.pipewire = 
    {
      enable = true;
      alsa.enable = true;
      alsa.support32Bit = true;
      pulse.enable = true;
      # If you want to use JACK applications, uncomment this
      jack.enable = true;

      # use the example session manager (no others are packaged yet so this is enabled by default,
      # no need to redefine it in your config for now)
      #media-session.enable = true;
    };

  # Enable touchpad support (enabled default in most desktopManager).
  # services.xserver.libinput.enable = true;

  # Define a user account. Don't forget to set a password with ‘passwd’.
  users.users.fern = 
    {
      isNormalUser = true;
      description = "Fern";
      extraGroups = [ "networkmanager" "wheel" ];
      packages = [];
    };

  # Install firefox.
  programs.firefox.enable = true;

  <<fonts>>
  
  programs.zsh.enable = true;
  users.defaultUserShell = pkgs.zsh;
  
  # List packages installed in system profile. To search, run:
  # $ nix search wget
  environment.systemPackages = with pkgs; 
    [
      gitkraken
      vscode
      nodejs_24
      alacritty
      chromium
      qbittorrent
    ];

  services.fprintd.enable = false;
  # Some programs need SUID wrappers, can be configured further or are
  # started in user sessions.
  # programs.mtr.enable = true;
  # programs.gnupg.agent = {
  #   enable = true;
  #   enableSSHSupport = true;
  # };

  # List services that you want to enable:

  # Enable the OpenSSH daemon.
  services.openssh.enable = true;

  # Open ports in the firewall.
  # networking.firewall.allowedTCPPorts = [ ... ];
  # networking.firewall.allowedUDPPorts = [ ... ];
  # Or disable the firewall altogether.
  # networking.firewall.enable = false;

  # This value determines the NixOS release from which the default
  # settings for stateful data, like file locations and database versions
  # on your system were taken. It‘s perfectly fine and recommended to leave
  # this value at the release version of the first install of this system.
  # Before changing this value read the documentation for this option
  # (e.g. man configuration.nix or on https://nixos.org/nixos/options.html).
  system.stateVersion = "24.11"; # Did you read the comment?
}
#+end_src

*** Home.nix
#+begin_src nix :tangle ./fern/home.nix
{ config, pkgs, stable, machine, ... }:
{
  # home.username = userSettings.name;
  # home.homeDirectory = "/home/" + userSettings.name;
  # home.stateVersion = "25.05";
 <<common-home-config>>
  programs.zsh.initContent = 
    ''
<<zshConfigFern>>
    '';
  
  home.packages = (with pkgs;
    [
      #Dev-Tools
      jetbrains.rider
      unityhub
      #ventoy
      bitwarden-desktop
      blender
      rclone
      
      protonvpn-gui
      localsend
      #Tools
      libreoffice
      gimp
      aseprite
      zotero
      obsidian
      stable.android-studio
      audacity

      #Media
      vlc
      obs-studio
      ffmpeg
      spotify
      parsec-bin
      
      #Communication
      discord
      whatsapp-for-linux
      telegram-desktop
      slack 
      zoom-us
      #Games
      steam
      lutris-unwrapped
      
      # Virtual Machines and wine
      libvirt
      wine
      virt-manager
      qemu
      uefi-run
      lxc
      swtpm
      bottles
      fprintd
    ]);

  <<emacs-config-wayland>>

  #   programs.zsh = {
  #     enable = true;
  #     enableCompletion = true;
  #     autosuggestion.enable = true;
  #     syntaxHighlighting.enable = true;

  #     shellAliases = {
  #       ll = "eza -l";
  #       la = "eza -la";
  #       ls = "eza";
  #       grep = "rg";
  #       find = "fd";
  #       e="emacsclient -c";
  #       update-config = "sudo nixos-rebuild switch --flake .#nixos";
  #       exp="/mnt/c/WINDOWS/explorer.exe .";
  #     };

  #     oh-my-zsh = {
  #       enable = true;
  #       plugins = [ "git" "sudo" ];
  #       theme = "robbyrussell";
  #     };
  
  #     initContent = ''
  # alias edituserconfig="code ~/.dotfiles/nix-devices-config/t490/home.nix"
  # alias editsystemconfig="code ~/.dotfiles/profiles/t490/configuration.nix"
  # alias updatesystemconfig="sudo nixos-rebuild switch --flake ~/.dotfiles/nix-devices-config"
  # alias updateuserconfig="home-manager switch --flake ~/.dotfiles/nix-devices-config#user" 
  # '';

  #   };


  home.sessionVariables = 
    {
      EMACSLOADINIT = "~/.dotfiles/emacs/init.el";
      # EDITOR = userSettings.editor;
      # SPAWNEDITOR = userSettings.spawnEditor;
      # BROWSER = userSettings.browser;
    };
  home.file.".emacs.d/init.el".source = /${config.home.homeDirectory}/${machine.dotfilesDir}/emacs/init.el;
}
#+end_src
* ZSH Config
** WSL Config
#+name: zshConfigWsl
#+begin_src bash
#DISPLAY=$(ip route | grep default | awk '{print $3}'):0.0
#LIBGL_ALWAYS_INDIRECT=1  
eval "$(direnv hook zsh)"
eval "$(zoxide init zsh)"
eval "$(oh-my-posh init zsh)"
export R_LIBS_SITE=$(ls -d /nix/store/*-r-*/library | tr '\n' ':')

PATH=/nix/store/5qng39wihv3lfgr03cf7mqbg4lpf4m45-cmake-3.30.5/bin:/mnt/c/Windows/System32/WindowsPowerShell/v1.0:$PATH

function isWinDir 
{
    case "$PWD/" in
        /mnt/*) return 0 ;;
        ,*) return 1 ;;
    esac
}

function lazygit 
{
    if isWinDir; then
	#  Use Windows `lazygit.exe` in Windows-mounted dirs
        command lazygit.exe "$@"
    else
	#  Use native Linux `lazygit` in native dirs
        command lazygit "$@"
    fi 
}
#+end_src


** Fern Config
#+name:zshConfigFern
#+begin_src bash
eval "$(zoxide init zsh)"
eval "$(oh-my-posh init zsh)"
#+end_src 
